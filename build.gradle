plugins {
    id 'idea'
    id 'application'
    id 'java-library'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '2.0.124'
}

ext {
    junitVersion = '5.10.2'
    jmhVersion = '1.37'
}


tasks.named('wrapper', Wrapper).configure {
    // Define wrapper values here so as to not have to always do so when updating gradlew.properties.
    // Switching this to Wrapper.DistributionType.ALL will download the full gradle sources that comes with
    // documentation attached on cursor hover of gradle classes and methods. However, this comes with increased
    // file size for Gradle. If you do switch this to ALL, run the Gradle wrapper task twice afterwards.
    // (Verify by checking gradle/wrapper/gradle-wrapper.properties to see if distributionUrl now points to `-all`)
    distributionType = Wrapper.DistributionType.BIN
}

version = "3.1.0"
group = "com.thunder.wildernessodysseyapi"

base {
    archivesName = "wildernessodysseyapi"
}

// Mojang ships Java 21 to end users starting in 1.20.5, so mods should target Java 21.
java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    // Specify the version of NeoForge to use.
    version = "21.1.216"

    parchment {
        mappingsVersion = project.parchment_mappings_version
        minecraftVersion = project.parchment_minecraft_version
    }

    // This line is optional. Access Transformers are automatically detected
    accessTransformers = project.files('src/main/templates/META-INF/accesstransformer.cfg')

    // Default run configurations.
    // These can be tweaked, removed, or duplicated as needed.
    runs {
        client {
            client()

            // Comma-separated list of namespaces to load gametests from. Empty = all namespaces.
            // Keep default (all) so both mod and vanilla GameTests register in dev.
        }

        server {
            server()
            programArgument '--nogui'
            // Keep default (all) so both mod and vanilla GameTests register in dev.
        }

        // This run config launches GameTestServer and runs all registered gametests, then exits.
        // By default, the server will crash when no gametests are provided.
        // The gametest system is also enabled by default for other run configs under the /test commands.
        gameTestServer {
            type = "gameTestServer"
            // Limit GameTests to our namespace by default so third-party mod tests do not fail the run.
            // Either property can be overridden from the command line, e.g.
            //   -Dforge.enabledGameTestNamespaces=
            //   -Dneoforge.enabledGameTestNamespaces=
            def forgeNamespaces = System.getProperty('forge.enabledGameTestNamespaces')
            def neoforgeNamespaces = System.getProperty('neoforge.enabledGameTestNamespaces')
            def enabledNamespaces = forgeNamespaces != null ? forgeNamespaces : neoforgeNamespaces
            // Include the vanilla namespace by default because our tests rely on the built-in
            // `minecraft:empty` template. Callers can still supply an empty string to run all tests.
            if (enabledNamespaces == null) {
                enabledNamespaces = "${project.mod_id},minecraft"
            }
            systemProperty 'forge.enabledGameTestNamespaces', enabledNamespaces
            systemProperty 'neoforge.enabledGameTestNamespaces', enabledNamespaces
        }

        data {
            data()

            // example of overriding the workingDirectory set in configureEach above, uncomment if you want to use it
            // gameDirectory = project.file('run-data')

            // Specify the modid for data generation, where to output the resulting resource, and where to look for existing resources.
            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
        }

        // applies to all the run configs above
        configureEach {
            // Recommended logging data for a userdev environment
            // The markers can be added/remove as needed separated by commas.
            // "SCAN": For mods scan.
            // "REGISTRIES": For firing of registry events.
            // "REGISTRYDUMP": For getting the contents of all registries.
            systemProperty 'forge.logging.markers', 'REGISTRIES'

            // Recommended logging level for the console
            // You can set various levels here.
            // Please read: https://stackoverflow.com/questions/2031163/when-to-use-the-different-log-levels
            logLevel = org.slf4j.event.Level.DEBUG
        }
    }

    mods {
        // define mod <-> source bindings
        // these are used to tell the game which sources are for which mod
        // mostly optional in a single mod project
        // but multi mod projects should define one per mod
        "${mod_id}" {
            sourceSet(sourceSets.main)
        }
    }
}

// Include resources generated by data generators.
sourceSets.main.resources { srcDir 'src/generated/resources' }

sourceSets {
    jmh {
        java.srcDir 'src/jmh/java'
        resources.srcDir 'src/jmh/resources'
        compileClasspath += sourceSets.main.output + configurations.runtimeClasspath
        runtimeClasspath += output + compileClasspath
    }
}

// Sets up a dependency configuration called 'localRuntime'.
// This configuration should be used instead of 'runtimeOnly' to declare
// a dependency that will be present for runtime testing but that is
// "optional", meaning it will not be pulled by dependents of this mod.
configurations {
    runtimeClasspath.extendsFrom localRuntime

    // Ensure bundled libraries are also available on the dev runtime classpath.
    // Without this, running from compiled classes (not the shaded jar) can miss
    // dependencies like Moshi and OkHttp, leading to NoClassDefFoundError at startup.
    runtimeClasspath.extendsFrom bundledLibs

    // Libraries that must be bundled directly into the mod jar so they are available at runtime.
    bundledLibs

    integrationTestImplementation.extendsFrom implementation, testImplementation
    integrationTestRuntimeOnly.extendsFrom runtimeOnly, testRuntimeOnly

    jmhImplementation.extendsFrom implementation
    jmhRuntimeOnly.extendsFrom runtimeOnly

    schemTestImplementation.extendsFrom implementation, testImplementation
    schemTestRuntimeOnly.extendsFrom runtimeOnly, testRuntimeOnly
}

// Make sure Gradle run configurations (JavaExec tasks) also see bundled libraries when
// launching directly from compiled classes, matching the crash log path
// `/build/classes/java/main/`.
tasks.withType(JavaExec).configureEach {
    classpath += configurations.bundledLibs
}


repositories {
    maven {
        url "https://cursemaven.com"
    }
}

dependencies {

    implementation "curse.maven:worldedit-225608:5830452"
    implementation "curse.maven:tick-tok-lib-1298954:7382106"
    implementation "curse.maven:create-328085:7408951"
    implementation "curse.maven:regions-unexplored-659110:6273893"
    implementation "curse.maven:terrablender-neoforge-940057:6054947"
    implementation "curse.maven:geckolib-388172:7023453"
    implementation "curse.maven:supplementaries-412082:7114487"
    implementation "curse.maven:multipart-machines-cooking-885587:5686941"
    implementation "curse.maven:locate-fixer-1301683:6762552"
    implementation 'org.yaml:snakeyaml:2.2'
    implementation "curse.maven:selene-499980:7113115"
    implementation "curse.maven:amendments-896746:7054319"
    implementation "curse.maven:spark-361579:6225208"
    implementation "curse.maven:sodium-394468:6382651"
    implementation "curse.maven:irisshaders-455508:6661598"
    implementation "curse.maven:nova-apii-1248874:7419137"
    implementation 'com.github.luben:zstd-jni:1.5.7-6'

    // AI advisory helpers (HTTP client and fault tolerance)
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    implementation 'com.squareup.okio:okio:3.9.0'
    implementation 'io.github.resilience4j:resilience4j-circuitbreaker:2.2.0'

    // Bundle third-party libraries into the final mod jar to avoid missing-class errors at runtime.
    bundledLibs 'com.squareup.okhttp3:okhttp:4.12.0'
    bundledLibs 'com.squareup.okio:okio:3.9.0'
    bundledLibs 'io.github.resilience4j:resilience4j-circuitbreaker:2.2.0'
    bundledLibs 'com.github.luben:zstd-jni:1.5.7-6'
    bundledLibs 'org.yaml:snakeyaml:2.2'

    testImplementation platform("org.junit:junit-bom:${junitVersion}")
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    integrationTestImplementation platform("org.junit:junit-bom:${junitVersion}")
    integrationTestImplementation 'org.junit.jupiter:junit-jupiter'

    jmhImplementation "org.openjdk.jmh:jmh-core:${jmhVersion}"
    jmhAnnotationProcessor "org.openjdk.jmh:jmh-generator-annprocess:${jmhVersion}"

    // Example optional mod dependency with JEI
    // The JEI API is declared for compile time use, while the full JEI artifact is used at runtime
    // compileOnly "mezz.jei:jei-${mc_version}-common-api:${jei_version}"
    // compileOnly "mezz.jei:jei-${mc_version}-neoforge-api:${jei_version}"
    // We add the full version to localRuntime, not runtimeOnly, so that we do not publish a dependency on it
    // localRuntime "mezz.jei:jei-${mc_version}-neoforge:${jei_version}"

    // Example mod dependency using a mod jar from ./libs with a flat dir repository
    // This maps to ./libs/coolmod-${mc_version}-${coolmod_version}.jar
    // The group id is ignored when searching -- in this case, it is "blank"
    // implementation "blank:coolmod-${mc_version}:${coolmod_version}"

    // Example mod dependency using a file as dependency
    // implementation files("libs/coolmod-${mc_version}-${coolmod_version}.jar")

    // Example project dependency using a sister or child project:
    // implementation project(":myproject")

    // For more info:
    // http://www.gradle.org/docs/current/userguide/artifact_dependencies_tutorial.html
    // http://www.gradle.org/docs/current/userguide/dependency_management.html
}

// This block of code expands all declared replace properties in the specified resource targets.
// A missing property will result in an error. Properties are expanded using ${} Groovy notation.
var generateModMetadata = tasks.register("generateModMetadata", ProcessResources) {
    var replaceProperties = [
            minecraft_version      : "1.21.1",
            minecraft_version_range: "1.21,1.21.1",
            neo_version            : "21.1.133",
            neo_version_range      : "21.1.133,",
            loader_version_range   : "5.0.3",
            mod_id                 : "wildernessodysseyapi",
            mod_name               : "Wilderness Odyssey API",
            mod_license            : "APR",
            mod_version            : "3.1.0",
            mod_authors            : "thunderrock424242",
            mod_description        : "a api for my modpack"
    ]
    inputs.properties replaceProperties
    expand replaceProperties
    from "src/main/templates"
    into "build/generated/sources/modMetadata"
}
// Include the output of "generateModMetadata" as an input directory for the build
// this works with both building through Gradle and the IDE.
sourceSets.main.resources.srcDir generateModMetadata
// To avoid having to run "generateModMetadata" manually, make it run on every project reload
neoForge.ideSyncTask generateModMetadata

tasks.register('obfuscateFiles', JavaExec) {
    group = "obfuscation"
    description = "Runs the in-place obfuscation (XOR) on two hardcoded files."

    // Use the fully qualified main class name:
    mainClass.set('com.thunder.wildernessodysseyapi.FileSecurity.SaferInPlaceObfuscateMulti')

    // The more traditional way to set the classpath in build.gradle (Groovy DSL):
    classpath = sourceSets.main.runtimeClasspath
}

// Package bundled libraries directly into the mod jar so they are available when Minecraft loads the mod.
tasks.named('jar', Jar).configure {
    // Ensure all mod resources (including built-in datapacks) are packaged into the jar.
    from(sourceSets.main.resources)

    from {
        configurations.bundledLibs.collect { it.isDirectory() ? it : zipTree(it) }
    }
    // Prevent duplicate resources when merging jars.
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            from components.java
            groupId = 'com.thunder'
            artifactId = 'wildernessodysseyapi'
            version = '3.1.0'
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Thunderrock424242/Wilderness-Odyssey-API")
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()
}

tasks.register('jmh', JavaExec) {
    group = 'benchmark'
    description = 'Runs JMH benchmarks for NBT serialization/deserialization.'
    classpath = sourceSets.jmh.runtimeClasspath
    mainClass.set('org.openjdk.jmh.Main')
    jvmArgs += ['-Djmh.ignoreLock=true']
    dependsOn tasks.named('compileJmhJava')
}

tasks.register('profileChunkWorkload', JavaExec) {
    group = 'performance'
    description = 'Executes the chunk workload driver with JFR and optional async-profiler attached.'
    classpath = sourceSets.main.runtimeClasspath
    mainClass.set('com.thunder.wildernessodysseyapi.chunk.ChunkWorkloadDriver')
    dependsOn tasks.named('classes')

    doFirst {
        def profileDir = layout.buildDirectory.dir('reports/profile').get().asFile
        profileDir.mkdirs()
    }

    jvmArgs += [
            "-XX:+UnlockDiagnosticVMOptions",
            "-XX:StartFlightRecording=filename=${layout.buildDirectory.file('reports/profile/chunk-workload.jfr').get().asFile.absolutePath},settings=profile,duration=30s"
    ]

    def asyncProfilerHome = System.getenv('ASYNC_PROFILER_HOME')
    if (asyncProfilerHome) {
        def asyncOutput = layout.buildDirectory.file('reports/profile/chunk-workload-async.svg').get().asFile
        jvmArgs += "-agentpath:${asyncProfilerHome}/build/libasyncProfiler.so=start,event=cpu,file=${asyncOutput.absolutePath}"
    }
}


// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
